// set up a simple neutral simulation
initialize() {
	initializeSLiMModelType("nonWF");
	initializeMutationRate(0);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 1);
	initializeRecombinationRate(0);
	defineConstant('N', 3160);
	defineConstant('CHILDS_PER_IND', 2);
	//defineConstant('EXTREME_VALUE', 10000);
	//defineConstant('EXTREME_PROB', 1/(100*CHILDS_PER_F*N));
	//setSeed(4421844);
	initializeTreeSeq();
}

reproduction(){
	for (_ in 1:CHILDS_PER_IND){
		child = subpop.addCloned(individual);
		child.tagF = max(0.0, individual.tagF + runif(1));
	}	
}

survival(){
	if (individual.age > 0){return F;}
	else {	
		if (individual.tagF < min_fitness){return F;} else {		
			return T;
		}
	}
}

// create a population of 500 individuals
1 early() {
	sim.addSubpop("p1", N);
	p1.individuals.tagF = 1.0;
	defineConstant('start_time', clock());
}

//NO REPRODUCTION at tick 1
//first -> reproduction -> early -> fitnessEffect -> survival -> late

1: early(){
	children = p1.individuals[p1.individuals.age == 0];
	sorted_fitness = sort(children.tagF, ascending=F);
	defineGlobal('min_fitness', sorted_fitness[N-1]);
}

2: late(){
	if (log(community.tick)/log(10) % 1 <= 1e-3){
		print("Tick " + community.tick + " attained in " + (clock() - start_time) + " seconds");
	}
}


(10*N):(200+10*N) late(){
	sim.treeSeqOutput("Brunet2 " + N + "Hgen" + community.tick + ".trees");
}

//L'ARBRE EST EN DOUBLE PARCE QUE HAPLOIDE !!!!!!!!!